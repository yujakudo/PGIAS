VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "shSpeciesMap"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True

'   リセットボタン
Private Sub btnChartReset_Click()
    Call doMacro(msgstr(msgReseting, name))
    Call clearInputs
    Call setMarker(ChartObjects(1))
    Call doMacro
End Sub

'   入力項目をクリアする
Private Sub clearInputs()
    Range(R_SpeciesMapTypeSelect).value = ""
    Range(R_SpeciesMapSpeciesSelect).value = ""
End Sub

'   マップの初期化ボタン
Private Sub btnMarkerColor_Click()
    Call doMacro(msgstr(msgMaking, name))
    Call MakeSpeciesMap
    Call doMacro
End Sub

'   タイプ選択・種族選択のセルの変更
Private Sub Worksheet_Change(ByVal Target As Range)
    Dim stype As Variant
    Dim rngType, rngSelect As Range
    
    Set rngType = Range(R_SpeciesMapTypeSelect)
    Set rngSelect = Range(R_SpeciesMapSpeciesSelect)
    '   タイプ選択・個体選択のセルの変更
    If Not Application.Intersect(Target, Union(rngType, rngSelect)) _
                                        Is Nothing Then
        Call doMacro(msgSettingMap)
        '   タイプ、個体選択を渡してマーカー設定
        stype = getTypesFromRange(rngType)
        Call setMarker(ChartObjects(1), stype, rngSelect.text)
        Call doMacro
    End If
End Sub


'   種族マップのリセット
Public Sub ResetSpeciesMap()
    Call btnChartReset_Click
End Sub

'   マップの作成
Public Sub MakeSpeciesMap()
    Dim cho As ChartObject
    Dim col(2) As Long
    Dim src As Range
    Dim settings As Object
    
    Set settings = getSettings(R_SpeciesMapSettings)
    Call clearInputs
    '   データ列の作成
    Call shSpeciesAnalysis1.sortNormally
    With shSpeciesAnalysis1.ListObjects(1)
        Set src = Union( _
            .ListColumns(settings(C_XAxis)).DataBodyRange, _
            .ListColumns(settings(C_YAxis)).DataBodyRange)
    End With
    Set cho = ChartObjects(1)
    Call SetSourceData(cho, src)
    Call setMarkerLabels(cho, shSpecies.cells(1, 2), _
                        Nothing, Nothing, settings(C_LabelAlign))
    Call setAxisLabel(cho, settings(C_XAxis), settings(C_YAxis))
End Sub


