VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "shList"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True

'   クリアボタン
Private Sub btnClear_Click()
    Dim lname As String
    lname = Range(LI_R_Select).Text
    If lname = "" Then Exit Sub
    Call doMacro(msgClearList)
    Call clearAList(lname)
    Call doMacro
End Sub

'   （ロケット団を除く）リスト全てをクリア
Public Sub clearAllList()
    Dim i As Integer
    Call doMacro(msgClearList)
    For i = 2 To shList.ListObjects.count
        Call clearAList(i)
    Next
    Call doMacro
End Sub

'   シートの変更
Private Sub Worksheet_Change(ByVal Target As Range)
    If Target.row > 1 Then Exit Sub
    If Target.column < Range(LI_R_Select).column And _
        Target.column Mod (ListObjects(1).HeaderRowRange.count + 1) = 1 Then
            Call changeTitle
    End If
End Sub

'   ロケット団のパラメータ設定ボタン
Private Sub btnCalcRocketParams_Click()
    Call calcRocketTroupeParams
End Sub

'   ListObjectを得る。
'   identifierは番号か、テーブルの上左のタイトル文字列
Private Function getLo(ByVal identifier As Variant) As ListObject
    Dim i As Integer
    If IsNumeric(identifier) Then
        Set getLo = ListObjects(identifier)
        Exit Function
    End If
    For i = 1 To ListObjects.count
        If ListObjects(i).HeaderRowRange.cells(1, 1).Offset(-1, 0).Text = identifier Then
            Set getLo = ListObjects(i)
            Exit Function
        End If
    Next
End Function

'   リストのタイトルが変わった
Private Sub changeTitle()
    Dim i As Integer
    Dim lst As String
    For i = 1 To ListObjects.count
        If lst <> "" Then lst = lst & ","
        lst = lst & ListObjects(i).HeaderRowRange.cells(1, 1).Offset(-1, 0).Text
    Next
    With Range(LI_R_Select)
        .Validation.Delete
        .Validation.Add Type:=xlValidateList, Formula1:=lst
    End With
End Sub

'   ロケット団のパラメータ設定
Private Function calcRocketTroupeParams()
    Dim cel As Variant
    Call doMacro(msgstr(msgProcessing, Array(ListObjects(1).name, msgCalculate)))
    With ListObjects(1).ListColumns(LI_Category)
        For Each cel In .DataBodyRange
            Call calcRocketSpecies(cel)
        Next
    End With
    Call doMacro
End Function

'   セルの列にパラメータ設定
Private Sub calcRocketSpecies(ByVal cel As Range)
    Dim mem, species As String
    Dim attr As Variant
    Dim CPM As Double
    mem = cel.Text
    species = getColumn(LI_Species, cel).Text
    If species = "" Then Exit Sub
    attr = getSpcAttrs(species, Array("ATK", "DEF", "HP"))
    '   CPM
    CPM = 1.265020656
    If mem = RCT_L0 Then
        CPM = CPM * 1.25
    ElseIf mem = RCT_L1 Or mem = RCT_L2 Or mem = RCT_L3 Then
        CPM = CPM * 1.15
    End If
    '   Powerの算出
    attr(0) = (attr(0) + 15) * 2 * CPM
    attr(1) = (attr(1) + 15) * CPM
    attr(2) = ((attr(2) + 15) / 2 - 0.5) * CPM
    getColumn(LI_ATK, cel).Value = attr(0)
    getColumn(LI_DEF, cel).Value = attr(1)
    getColumn(LI_HP, cel).Value = attr(2)
End Sub

Private Sub clearAList(ByVal id As Variant)
    With getLo(id)
        .DataBodyRange.ClearContents
        .Resize Range(.HeaderRowRange, .HeaderRowRange.Offset(1, 0))
    End With
End Sub

