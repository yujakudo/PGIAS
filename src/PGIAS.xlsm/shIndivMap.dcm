VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "shIndivMap"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True

'   入力項目をクリアする
Private Sub clearInputs(Optional ByRef rngTbl As Range = Nothing)
    Dim prev As Boolean
    prev = Application.EnableEvents
    Application.EnableEvents = False
    Range(IMAP_R_TypeSelect).value = ""
    Range(IMAP_R_IndivSelect).value = ""
    chkShowPrediction.value = True
    If Not rngTbl Is Nothing Then
        Me.names(IMAP_R_Table).RefersTo = "='" & Me.name & "'!" & rngTbl.Address
    End If
    Application.EnableEvents = prev
End Sub

'   マップを作るボタン
Private Sub btnMakeMap_Click()
    Call MakeMap
End Sub

Public Sub MakeMap(Optional ByVal sequence As FMAP_FLAG = FMAP_ALL)
    Dim settings As Object
    Dim rngTbl As Range
    
    Set settings = getSettings(Range(IMAP_R_Settings))
    Set rngTbl = Range(IMAP_R_Table)
    Call makeIndivMap(rngTbl, settings, sequence)
    Call clearInputs(rngTbl)
End Sub

'   リセット
Private Sub btnReset_Click()
    Call doMacro(msgstr(msgReseting, name))
    Call setMarker(ChartObjects(1))
    Call doMacro
    Call clearInputs
End Sub

'   予測表示
Private Sub chkShowPrediction_Click()
    Call Worksheet_Change(Range(IMAP_R_IndivSelect))
End Sub

'   シートの変更
Private Sub Worksheet_Change(ByVal target As Range)
    Dim stype As Variant
    Dim rngType, rngSelect, rngSetting As Range
    
    Set rngType = Range(IMAP_R_TypeSelect)
    Set rngSelect = Range(IMAP_R_IndivSelect)
    Set rngSetting = Range(IMAP_R_Settings)
    
    '   タイプ選択・個体選択のセルの変更
    If Not Application.Intersect(target, Union(rngType, rngSelect)) _
                                        Is Nothing Then
        Call doMacro(msgSettingMap)
        '   タイプ、個体選択を渡してマーカー設定
        stype = getTypesFromRange(rngType)
        Call setMarker(ChartObjects(1), stype, _
                        rngSelect.text, chkShowPrediction.value)
        Call doMacro
    '   設定の変更
    ElseIf target.row > 1 And Not Application.Intersect(target, rngSetting) Is Nothing Then
        Call onChangeIndMapSettings(target, rngSetting)
    End If
End Sub

'   個体マップをすべてクリア
Public Sub ClearIndivMap()
    Call DeleteSourceData(ChartObjects(1))
    Call clearInputs
    Range(IMAP_R_Table).ClearContents
    Range(IMAP_R_MakingTime).ClearContents
End Sub


'   すべて初期化
Public Sub initSheet()
    Call ClearIndivMap
    Call initSettings
End Sub

'   設定初期化
Public Sub initSettings()
    Dim settings As Object
    Call doMacro(msgstr(msgInitializing, Me.name))
    Set settings = CreateObject("Scripting.Dictionary")
    settings.item(C_CpUpper) = ""
    settings.item(C_PrCpLower) = ""
    settings.item(C_XAxis) = IND_Endurance
    settings.item(C_XPrediction) = IND_prEndurance
    settings.item(C_YAxis) = left(IND_GymSpecialAtk1CDPS, Len(IND_GymSpecialAtk1CDPS) - 1) & "*"
    settings.item(C_YPrediction) = IND_prGymCDPS
    settings.item(C_LabelAlign) = 8
    settings.item(C_AutoTarget) = C_None
    settings.item(C_Level) = 40
    Call setSettings(Range(IMAP_R_Settings), settings)
    Call doMacro
End Sub

